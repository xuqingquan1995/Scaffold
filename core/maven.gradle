apply plugin: 'maven-publish'
apply plugin: 'com.github.dcendents.android-maven'

def siteUrl = 'https://github.com/xuqingquan1995/Scaffold'
def gitUrl = 'https://github.com/xuqingquan1995/Scaffold.git'

group = 'top.xuqingquan'
version = versionName

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += configurations.compile
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

javadoc {
    options {
        encoding "UTF-8"
        docEncoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/8/docs/api"
        failOnError false
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom.project {
            packaging 'aar'
            name 'scaffold'
            artifactId 'scaffold'
            description 'Android 基础开发框架，帮助开发者快速搭建基础模块'
            url siteUrl
            // Set your license
            licenses {
                license {
                    name 'MIT'
                    url 'http://www.opensource.org/licenses/mit-license.php'
                }
            }
            developers {
                developer {
                    id 'xuqingquan'
                    name '许清泉'
                    email 'xuqingquan1995@gmail.com'
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'top.xuqingquan'
                artifactId = 'scaffold'
                version = versionName
            }
        }
    }
}